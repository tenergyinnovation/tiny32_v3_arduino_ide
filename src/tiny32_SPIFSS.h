
#include <Arduino.h>
#include <SPIFFS.h>
#include <ArduinoJson.h>

extern tiny32_v3 mcu;

/**************************************/
/*           define function          */
/**************************************/
void listDir(fs::FS &fs, const char *dirname, uint8_t levels);
void readFile(fs::FS &fs, const char *path);
void writeFile(fs::FS &fs, const char *path, const char *message);
void appendFile(fs::FS &fs, const char *path, const char *message);
void renameFile(fs::FS &fs, const char *path1, const char *path2);
void deleteFile(fs::FS &fs, const char *path);
void testFileIO(fs::FS &fs, const char *path);


/***********************************************************************
 * FUNCTION:    listDir
 * DESCRIPTION: list directory from SPIFFS
 * PARAMETERS:  fs, dirname, levels
 * RETURNED:    nothing
 ***********************************************************************/
void listDir(fs::FS &fs, const char *dirname, uint8_t levels)
{
    Serial.printf("Listing directory: %s\r\n", dirname);

    File root = fs.open(dirname);
    if (!root)
    {
        Serial.println("- failed to open directory");
        return;
    }
    if (!root.isDirectory())
    {
        Serial.println(" - not a directory");
        return;
    }

    File file = root.openNextFile();
    while (file)
    {
        if (file.isDirectory())
        {
            Serial.print("  DIR : ");
            Serial.println(file.name());
            if (levels)
            {
                listDir(fs, file.path(), levels - 1);
            }
        }
        else
        {
            Serial.print("  FILE: ");
            Serial.print(file.name());
            Serial.print("\tSIZE: ");
            Serial.println(file.size());
        }
        file = root.openNextFile();
    }
}

/***********************************************************************
 * FUNCTION:    readFile
 * DESCRIPTION: read File from SPIFFS
 * PARAMETERS:  fs, path
 * RETURNED:    nothing
 ***********************************************************************/
void readFile(fs::FS &fs, const char *path)
{
    Serial.printf("Reading file: %s\r\n", path);

    File file = fs.open(path);
    if (!file || file.isDirectory())
    {
        Serial.println("- failed to open file for reading");
        return;
    }

    Serial.println("- read from file:");
    while (file.available())
    {
        Serial.write(file.read());
    }
    file.close();
}

/***********************************************************************
 * FUNCTION:    writeFile
 * DESCRIPTION: write data to File for SPIFFS
 * PARAMETERS:  fs, path, message
 * RETURNED:    string
 ***********************************************************************/
void writeFile(fs::FS &fs, const char *path, const char *message)
{
    Serial.printf("Writing file: %s\r\n", path);

    File file = fs.open(path, FILE_WRITE);
    if (!file)
    {
        Serial.println("- failed to open file for writing");
        return;
    }
    if (file.print(message))
    {
        Serial.println("- file written");
    }
    else
    {
        Serial.println("- write failed");
    }
    file.close();
}

/***********************************************************************
 * FUNCTION:    appendFile
 * DESCRIPTION: appen data to File for SPIFFS
 * PARAMETERS:  fs, path, message
 * RETURNED:    nothing
 ***********************************************************************/
void appendFile(fs::FS &fs, const char *path, const char *message)
{
    Serial.printf("Appending to file: %s\r\n", path);

    File file = fs.open(path, FILE_APPEND);
    if (!file)
    {
        Serial.println("- failed to open file for appending");
        return;
    }
    if (file.print(message))
    {
        Serial.println("- message appended");
    }
    else
    {
        Serial.println("- append failed");
    }
    file.close();
}

/***********************************************************************
 * FUNCTION:    renameFile
 * DESCRIPTION: Remane File for SPIFFS
 * PARAMETERS:  fs, path1, path2
 * RETURNED:    nothing
 ***********************************************************************/
void renameFile(fs::FS &fs, const char *path1, const char *path2)
{
    Serial.printf("Renaming file %s to %s\r\n", path1, path2);
    if (fs.rename(path1, path2))
    {
        Serial.println("- file renamed");
    }
    else
    {
        Serial.println("- rename failed");
    }
}

/***********************************************************************
 * FUNCTION:    deleteFile
 * DESCRIPTION: Delete File for SPIFFS
 * PARAMETERS:  fs, path
 * RETURNED:    nothing
 ***********************************************************************/
void deleteFile(fs::FS &fs, const char *path)
{
    Serial.printf("Deleting file: %s\r\n", path);
    if (fs.remove(path))
    {
        Serial.println("- file deleted");
    }
    else
    {
        Serial.println("- delete failed");
    }
}

/***********************************************************************
 * FUNCTION:    testFileIO
 * DESCRIPTION: test File IO for SPIFFS
 * PARAMETERS:  fs, path
 * RETURNED:    nothing
 ***********************************************************************/
void testFileIO(fs::FS &fs, const char *path)
{
    Serial.printf("Testing file I/O with %s\r\n", path);

    static uint8_t buf[512];
    size_t len = 0;
    File file = fs.open(path, FILE_WRITE);
    if (!file)
    {
        Serial.println("- failed to open file for writing");
        return;
    }

    size_t i;
    Serial.print("- writing");
    uint32_t start = millis();
    for (i = 0; i < 2048; i++)
    {
        if ((i & 0x001F) == 0x001F)
        {
            Serial.print(".");
        }
        file.write(buf, 512);
    }
    Serial.println("");
    uint32_t end = millis() - start;
    Serial.printf(" - %u bytes written in %u ms\r\n", 2048 * 512, end);
    file.close();

    file = fs.open(path);
    start = millis();
    end = start;
    i = 0;
    if (file && !file.isDirectory())
    {
        len = file.size();
        size_t flen = len;
        start = millis();
        Serial.print("- reading");
        while (len)
        {
            size_t toRead = len;
            if (toRead > 512)
            {
                toRead = 512;
            }
            file.read(buf, toRead);
            if ((i++ & 0x001F) == 0x001F)
            {
                Serial.print(".");
            }
            len -= toRead;
        }
        Serial.println("");
        end = millis() - start;
        Serial.printf("- %u bytes read in %u ms\r\n", flen, end);
        file.close();
    }
    else
    {
        Serial.println("- failed to open file for reading");
    }
}

/***********************************************************************
 * FUNCTION:    getdataFile
 * DESCRIPTION: get data of File from SPIFFS
 * PARAMETERS:  fs, path
 * RETURNED:    nothing
 ***********************************************************************/
String getdataFile(fs::FS &fs, const char *path)
{
    String _string = "";
    const int bufferSize = 40000; // Adjust the buffer size as needed
    char buffer[bufferSize];      // Character array to store the data
    int bufferIndex = 0;          // Index to keep track of the current position in the buffer
    boolean newData = false;      // Flag to indicate when new data has been received

    Serial.printf("Reading file: %s\r\n", path);

    File file = fs.open(path);
    if (!file || file.isDirectory())
    {
        Serial.println("- failed to open file for reading");
        return _string;
    }

    Serial.println("- read from file:");
    while (file.available())
    {

        char incomingChar = file.read();
        _string += incomingChar;
    }
    file.close();
    return _string;
}
